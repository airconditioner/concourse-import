apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
   mavenCentral()
}

dependencies {
    compile 'org.cinchapi:concourse:0.4.0-rc1'
    compile ('org.cinchapi:concourse-config:1.0.4'){
    	exclude group: 'org.cinchapi', module:'concourse'
    }
    compile ('org.cinchapi:concourse-cli:1.1.0'){
    	exclude group: 'org.cinchapi', module:'concourse'
    }
    compile 'com.google.guava:guava:15.0'
    compile 'com.intellij:annotations:12.0'
    compile 'com.beust:jcommander:1.32'
    compile 'jline:jline:2.11'
    
    testCompile 'junit:junit:4.11'
    testCompile ('org.cinchapi:concourse-test:1.1.1'){
    	exclude group: 'org.cinchapi', module:'concourse'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.8'
}

def getVersion = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'bash', 'version.sh'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

ext.globalVersion  = getVersion()

group = 'org.cinchapi'
version = globalVersion

jar {
    manifest {
        attributes("Specificiation-Title": "Concourse Import Framework", "Specificiation-Version": version, "Implementation-Version": version)
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

signing {
    sign configurations.archives
}

def getMaven = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'cat', '.maven'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }

project.ext.maven = getMaven()

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: project.maven) {
              authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.version = '2.0.0'

            pom.project {
               name 'Concourse Import Framework'
               packaging 'jar'
               description 'A framework for importing raw data into Concourse'
               url 'http://cinchapi.org/concourse'

               scm {
                   url 'git@github.com:cinchapi/concourse-import.git'
                   connection 'git@github.com:cinchapi/concourse-import.git'
                   developerConnection 'git@github.com:cinchapi/concourse-import.git'
               }

               licenses {
                   license {
                       name 'The MIT License'
                       url 'http://opensource.org/licenses/MIT'
                       distribution 'repo'
                   }
               }

               developers {
                   developer {
                       id 'jnelson'
                       name 'Jeff Nelson'
                   }
               }
           }
        }
    }
}

